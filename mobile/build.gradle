repositories {
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        applicationId project.ext.applicationId
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        versionCode project.ext.versionCode()
        versionName project.ext.versionName()
    }

    buildTypes {
        debug {
            // Todo: Robolectric 3.0 cannot handle applicationIdSuffix.  Update when robolectic fixes
            // applicationIdSuffix ".debug"
            testCoverageEnabled true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {

        base {
        }

        qaTest {
            renderscriptNdkModeEnabled true
            renderscriptTargetApi project.ext.targetSdkVersion
            renderscriptSupportModeEnabled true
            dependencies {
                qaTestCompile project(':Mobile_eXplorer_Agent_AndroidStudio')
            }
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    // Do not allow release builds of QaTest
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release') && variant.getFlavors().get(0).name.equals('qaTest')) {
            variant.setIgnore(true);
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.0.0'
    compile 'com.android.support:cardview-v7:22.0.0'


    // Dagger - Dependency Injection
    def daggerVersion = '1.2.2'
    compile "com.squareup.dagger:dagger:$daggerVersion"
    provided "com.squareup.dagger:dagger-compiler:$daggerVersion"

    // Butter knife - View 'Injection'
    compile 'com.jakewharton:butterknife:6.1.0'

    // Otto - Event bus
    compile 'com.squareup:otto:1.3.6'

    //Retrofit
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'

    // Timber - Logging
    compile 'com.jakewharton.timber:timber:2.5.0'

    // Unit Conversion
    compile ('org.jscience:jscience:4.3.1') {
        exclude group: 'org.javolution', module: 'javolution'
    }


    // ================== TESTING LIBRARIES ======================
    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
    testCompile ('com.squareup:fest-android:1.0.8') {
        exclude group: 'com.android.support'
    }
    testCompile('org.robolectric:robolectric:3.0-rc2') {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
        exclude group: 'commons-logging', module: 'commons-logging'
    }
}